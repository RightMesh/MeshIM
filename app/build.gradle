plugins {
    id 'com.android.application'
    id 'io.left.rightmesh.rightmesh-plugin' version '1.8.1'
    id 'com.google.protobuf'
}

rightmesh {
    appKey = rightmesh_meshim_key
    username = rightmesh_devportal_username
    password = rightmesh_devportal_password
}

preBuild.dependsOn("rightmesh")
repositories {
    mavenCentral()
    jcenter()
    google()
    maven {
        url "http://artifactory.rightmesh.io/artifactory/maven"
        credentials {
            username rightmesh_devportal_username
            password rightmesh_devportal_password
        }
    }
    maven{
        url "https://dl.bintray.com/ethereum/maven"
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'io.left.meshim'
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 3
        versionName "0.2.1"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/src/main/schemas".toString()]
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/src/main/schemas".toString())
        main { assets.srcDirs = ['src/main/assets', 'src/main/assets/2'] }
    }
    productFlavors {
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'

    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'io.left.rightmesh:lib-rightmesh-android:0.10.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    implementation 'com.airbnb.android:lottie:2.5.0-rc1'
}
